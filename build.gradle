buildscript {
  apply from: rootProject.file('dependencies.gradle')
  apply from: rootProject.file('app/app-dependencies.gradle')

  repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath sdkBuildDep.androidPlugin
    classpath sdkBuildDep.dexCountPlugin
    classpath sdkBuildDep.credentialsPlugin
    classpath sdkBuildDep.jacocoPlugin
    classpath demoAppBuildDep.crashlyticsPlugin
  }
}

subprojects {
  def isRootProject = rootProject.name.equals(name)

  repositories {
    if (rootProject.hasProperty('NEXUS_USER') &&
        rootProject.hasProperty('NEXUS_PASSWORD')) {
      maven {
        url "https://repo.bazaarvoice.com/nexus/content/repositories/snapshots"
        credentials {
          username NEXUS_USER
          password NEXUS_PASSWORD
        }
      }
    }
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }

  if (!isRootProject) {
    plugins.apply('checkstyle')

    task('checkstyle', type: Checkstyle) {
      configFile rootProject.file('checkstyle.xml')
      source 'src/main/java'
      ignoreFailures false
      showViolations true
      include '**/*.java'

      classpath = files()
    }
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    if (!isRootProject) {
      tasks.findByName('check').dependsOn('checkstyle')
    }
  }
}
